buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5' }
}

plugins {
    id "com.github.jruby-gradle.jar" version "0.1.2"
    // https://github.com/jruby-gradle/jruby-gradle-jar-plugin/issues/18
    id "com.github.jruby-gradle.base" version "0.1.3"
    id "com.github.johnrengelman.shadow" version "1.1.2"
}
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'


group = 'com.github.jruby-gradle'
version = '0.1.0'

if (System.env.RELEASE != '1') {
    version = "${version}-SNAPSHOT"
}
sourceCompatibility = 1.7

repositories {
    jcenter()
    mavenLocal()

    // Repositories for Storm dependencies
    mavenCentral()
    maven { url 'http://clojars.org/repo/' }
    maven { url 'http://conjars.org/repo/' }
}

configurations {
    jrubyStorm
    jrubyStormLocal.extendsFrom jrubyStorm
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile group: 'com.github.jruby-gradle', name: 'jruby-gradle-plugin', version: '0.1.3+'
    compile group: 'com.github.jruby-gradle', name: 'jruby-gradle-jar-plugin', version: '0.1.2+'

    jrubyStorm (group: 'com.github.jruby-gradle',
                name: 'redstorm',
                version: '0.7.0-SNAPSHOT') {
        exclude module: 'storm-core'
    }

    jrubyStormLocal group: 'org.apache.storm',
            name: 'storm-core',
            version: '0.9.1-incubating',
            force: true

    testCompile ("org.spockframework:spock-core:0.7-groovy-${gradle.gradleVersion.startsWith('1.')?'1.8':'2.0'}") {
        exclude module : 'groovy-all'
    }
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

task localTest(type: JavaExec) {
  classpath configurations.jrubyStormLocal.asPath
  main 'redstorm.TopologyLauncher'
  args 'local', './topologies/hello_world_topology'
}

shadowJar {
    exclude '*.sw*', '*.gitkeep', '*.md'

    from 'topologies'

    // note that zipTree call is wrapped in closure so that configuration
    // is only resolved at execution time. This will take the embeds
    // from within the `jrubyStorm` configuration and dump them into the war
    from {
        project.configurations.jrubyStorm.collect {
            project.zipTree(it)
        }
    }

    jruby {
        // Use the default GEM installation directory
        defaultGems()
        mainClass 'redstorm.TopologyLauncher'
    }
}

// Ensure we don't fail in CI or on a system without these values set in
// ~/.gradle/gradle.properties
if (!hasProperty( 'bintrayUser' ))
  ext.bintrayUser = ''

if (!hasProperty( 'bintrayKey' ))
  ext.bintrayKey = ''

bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'jruby-gradle'
        repo = 'plugins'
        name = 'jruby-gradle-storm-plugin'
        labels = ['jruby','storm','java']

        version {
            name = project.version
            vcsTag = "v${project.version}"
            attributes = ['gradle-plugin' : 'com.github.jruby-gradle:com.github.jruby-gradle:jruby-gradle-storm-plugin']
            desc = 'This plugin encapsulates Storm topology building functionality for JRuby Gradle projects'
        }
    }
}
bintrayUpload.dependsOn assemble

// vim: ft=groovy
