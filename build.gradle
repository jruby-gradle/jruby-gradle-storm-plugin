buildscript {
    repositories {
        jcenter()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2"
        classpath "org.ysb33r.gradle:gradletest:0.5.4"
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.ysb33r.gradletest'
apply plugin: 'codenarc'
apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.github.jruby-gradle'
version = '0.2.0'
defaultTasks 'check', 'assemble'

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    ['jruby-gradle-plugin', 'jruby-gradle-jar-plugin'].each { String plugin ->
        String pluginDependency = "com.github.jruby-gradle:${plugin}:${jrubyGradleMinVersion}"
        compile pluginDependency
        gradleTest pluginDependency
    }

    compile 'com.github.jengelman.gradle.plugins:shadow:1.2.2+'

    testCompile ("org.spockframework:spock-core:0.7-groovy-${gradle.gradleVersion.startsWith('1.')?'1.8':'2.0'}") {
        exclude module : 'groovy-all'
    }
}

codenarc {
    sourceSets = [sourceSets.main]
    configFile = file('gradle/codenarc.xml')
}

test {
    testLogging {
        showStandardStreams = true
        exceptionFormat "full"
    }
}

gradleTest {
    versions '2.0', '2.2', '2.4', '2.6'
    dependsOn jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}


bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'jruby-gradle'
        repo = 'plugins'
        name = 'jruby-gradle-storm-plugin'
        labels = ['jruby','storm','java']

        version {
            name = project.version
            vcsTag = "v${project.version}"
            attributes = ['gradle-plugin' : 'com.github.jruby-gradle.storm:com.github.jruby-gradle:jruby-gradle-storm-plugin']
            desc = 'This plugin encapsulates Storm topology building functionality for JRuby Gradle projects'
        }
    }
}
bintrayUpload.dependsOn assemble

// vim: ft=groovy
